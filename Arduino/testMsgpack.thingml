/**
We have seen in 1_helloWorld how to make a simple thing.
Now it is time to design two things that communicate together: this is the ping pong sample!
*/

/**
First, define the messages that make sense for this ping pong application...
Obviously, one ping and one pong message
*/
datatype UInt8<1> @c_type "uint8_t";
datatype UInt16<2> @c_type "uint16_t";

thing fragment PingPongMsgs {
	message ping(u : UInt16) @code "16";
	message ping1(u : UInt8);
	message pong();
}

/**
Now we implement a basic ping server. It will sends back a pong message whenever it receives a ping message
*/
thing PingServer includes PingPongMsgs {

	provided port ping_service {
		sends pong
		receives ping, ping1, pong
	}

	statechart PongServerMachine init Active {

        on entry print "Ping Server Started!\n"

		state Active {
			
			internal event e : ping_service?ping
			action do
				print "ping: "
				print e.u
				print "\n"
				ping_service!pong()
			end
			
		}
	}
}

/**
Now we implement a ping client. 
It will first (in the Ping state) sends a ping message and wait for a pong message
When the pong message is received, we will go to the Pong state, acknoledging (print) that we have received the pong
Finally, we will go the Stop state and print Bye.
*/
thing PingClient includes PingPongMsgs {

	required port ping_service {
		receives pong
		sends ping
	}
	
	statechart PingClientMachine init Ping {
	
	on entry print "Ping Client Started!\n"
	
		state Ping {
			
			on entry do
				print "Send Ping..." 
				ping_service!ping(255)
			end
				
			
			transition PONG -> Pong 
			event ping_service?pong
			
		}
		
		state Pong {
			on entry print "Got pong!\n"
			
			transition -> Stop
			
		}
		
		state Stop {
			on entry print "Bye.\n"
		}
		
	}
	
}

/**
Again, that simple ping pong application is fully platform-independent.
TODO: compile and run this configuration for the different platforms: C/Linux, Java, JavaScript and Arduino (if you have one)
@nlg "NoBufSerial"
@serial_start_byte "18"
@serial_stop_byte "19"
@serial_escape_byte "125"
*/
protocol Serial1
@nlg "NoBufSerial"
@serializer "msgpack"
;
protocol Serial2
@nlg "NoBufSerial"
@serializer "msgpack"
;

configuration testMsgpackCfg 
@arduino_stdout "Serial"
{

	instance client: PingClient
	instance server: PingServer

	connector client.ping_service over Serial2
	connector server.ping_service over Serial1

}
