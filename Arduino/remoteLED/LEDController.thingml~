
import "../../lib/_Datatypes.thingml"
import "Msgs.thingml"

configuration LEDControllerCfg
@c_header "#include \"/home/sintef/Documents/ArduinoSerialForward/ArduinoSerialForward.h\""
{
	instance lc : LEDController
	instance out : Out
	@c_external_init "setupArduinoSerialForward(9600)"
	@c_external_listen "readSerial()"
	@c_external_set_listener_id "setListenerID"
	
	connector lc.LEDport => out.Serial
	
}

thing Out includes Msgs
@remote "true"
{
	provided port Serial {
		receives LEDControllerReady
		sends LEDON, LEDOFF
	}
}

thing LEDController includes Msgs
@c_header "#include <SoftwareSerial.h>"
@c_global "SoftwareSerial mySerial(10, 11);"
{
	property LEDpin : UInt8 = 7
	
	required port LEDport {
		sends LEDControllerReady
		receives LEDON, LEDOFF
	}
	
	statechart LEDControllerChart init Init {
		state Init {
			transition -> Idle action do
				'mySerial.begin(9600);'
				'mySerial.println("[Controller] Init");'
				'pinMode('& LEDpin &', OUTPUT);'
			end
		}
		
		state Idle {
			internal event LEDport?LEDON action do
				'mySerial.println("[Controller] LEDON");'
				'digitalWrite('& LEDpin &', HIGH);'
			end
			
			internal event LEDport?LEDOFF action do
				'mySerial.println("[Controller] LEDOFF");'
				'digitalWrite('& LEDpin &', LOW);'
			end
		}
	}
}

